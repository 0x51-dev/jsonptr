json-pointer          = *( "/" reference-token )
reference-token       = *( unescaped / escaped )
unescaped             = %x00-2E / %x30-7D / %x7F-FFFC / %xFFFE-10FFFF
    ; %x2F ('/') and %x7E ('~') are excluded from 'unescaped'
escaped               = "~" ( "0" / "1" )
    ; representing '~' and '/', respectively

relative-json-pointer = origin-specification ( "#" / json-pointer )
    ; json-pointer from RFC 6901
origin-specification  = non-negative-integer [ index-manipulation ]
index-manipulation    = ( "+" / "-" ) positive-integer
non-negative-integer  = "0" / positive-integer
positive-integer      = %x31-39 *%x30-39
    ; digits without a leading zero
