json-pointer          = *( "/" reference-token )
reference-token       = *( unescaped / escaped )
unescaped             = %x00-2E / %x30-7D / %x7F-FFFC / %xFFFE-10FFFF
    ; %x2F ('/') and %x7E ('~') are excluded from 'unescaped'
escaped               = "~" ( "0" / "1" )
    ; representing '~' and '/', respectively
relative-json-pointer =  non-negative-integer ([index-manipulation] json-pointer / "#")
index-manipulation    =  ("+" / "-") non-negative-integer
non-negative-integer  =  %x30 / %x31-39 *( %x30-39 )
    ; "0", or digits without a leading "0"
